"""
    struct DefaultContext <: AbstractContext end

The `DefaultContext` is used by default to compute log the joint probability of the data 
and parameters when running the model.
"""
struct DefaultContext <: AbstractContext end

"""
    struct PriorContext{Tvars} <: AbstractContext
        vars::Tvars
    end

The `PriorContext` enables the computation of the log prior of the parameters `vars` when 
running the model.
"""
struct PriorContext{Tvars} <: AbstractContext
    vars::Tvars
end
PriorContext() = PriorContext(nothing)

"""
    struct LikelihoodContext{Tvars} <: AbstractContext
        vars::Tvars
    end

The `LikelihoodContext` enables the computation of the log likelihood of the parameters when 
running the model. `vars` can be used to evaluate the log likelihood for specific values 
of the model's parameters. If `vars` is `nothing`, the parameter values inside the `VarInfo` will be used by default.
"""
struct LikelihoodContext{Tvars} <: AbstractContext
    vars::Tvars
end
LikelihoodContext() = LikelihoodContext(nothing)

"""
    struct MiniBatchContext{Tctx, T} <: AbstractContext
        ctx::Tctx
        loglike_scalar::T
    end

The `MiniBatchContext` enables the computation of 
`log(prior) + s * log(likelihood of a batch)` when running the model, where `s` is the 
`loglike_scalar` field, typically equal to `the number of data points / batch size`. 
This is useful in batch-based stochastic gradient descent algorithms to be optimizing 
`log(prior) + log(likelihood of all the data points)` in the expectation.
"""
struct MiniBatchContext{Tctx, T} <: AbstractContext
    ctx::Tctx
    loglike_scalar::T
end
function MiniBatchContext(ctx = DefaultContext(); batch_size, npoints)
    return MiniBatchContext(ctx, npoints/batch_size)
end

struct EvaluationContext{NT, T, Ctx} <: AbstractContext
    θ::NT
    logp::Base.RefValue{T}
    ctx::Ctx
end
EvaluationContext{T}(θ, ctx = DefaultContext()) where {T<:Real} = EvaluationContext{typeof(θ), T, typeof(ctx)}(θ, Ref(zero(T)), ctx)
EvaluationContext(θ, ctx = DefaultContext()) = EvaluationContext{Float64}(θ, ctx)

function acclogp!(ctx::EvaluationContext, logp)
    ctx.logp[] += logp
    return ctx
end

getlogp(ctx::EvaluationContext) = ctx.logp[]
